{
  parserClass="se.ch.HAnS.fileAnnotation.parser.FileAnnotationParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="FileAnnotation"
  psiImplClassSuffix="Impl"
  psiPackage="se.ch.HAnS.fileAnnotation.psi"
  psiImplPackage="se.ch.HAnS.fileAnnotation.psi.impl"

  elementTypeHolderClass="se.ch.HAnS.fileAnnotation.psi.FileAnnotationTypes"
  elementTypeClass="se.ch.HAnS.fileAnnotation.psi.FileAnnotationElementType"
  tokenTypeClass="se.ch.HAnS.fileAnnotation.psi.FileAnnotationTokenType"

  psiImplUtilClass="se.ch.HAnS.fileAnnotation.psi.impl.FileAnnotationPsiImplUtil"
}

fileAnnotationFile ::= (fileAnnotation NEWLINE*)*

fileAnnotation ::= fileReferences COLON? NEWLINE+ lpqReferences

fileReferences ::= (fileReference ((SPACE* CS)? SPACE* fileReference)* SPACE*)

fileReference ::= (CM fileName CM) | (fileName)

fileName ::= (STRING DOT STRING)|STRING

lpqReferences ::= (lpq ((SPACE* CS)? SPACE* lpq)* SPACE*)

//&begin[Referencing]
lpq ::= featureName (SEPARATOR featureName)* {
mixin="se.ch.HAnS.fileAnnotation.psi.impl.FileAnnotationNamedElementImpl"
implements="se.ch.HAnS.fileAnnotation.psi.FileAnnotationNamedElement"
methods=[getName setName getNameIdentifier]}
//&end[Referencing]

featureName ::= STRING